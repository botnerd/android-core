apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// Build config types set by the variants
final String CONFIG_TYPE_BOOLEAN = 'boolean'
//noinspection GroovyUnusedAssignment
final String CONFIG_TYPE_INT = 'int'
//noinspection GroovyUnusedAssignment
final String CONFIG_TYPE_LONG = 'long'
//noinspection GroovyUnusedAssignment
final String CONFIG_TYPE_STRING = 'String'

// Build config fields set by the variants
//noinspection GroovyUnusedAssignment
final String CONFIG_LOGGING_ENABLED = 'LOGGING_ENABLED'

group "$coreGroupId"
version = "$coreVersion"

ext {
    artifact = project.name

    publishedGroupId = "$coreGroupId"
    publishArtifactId = "$artifact"

    libraryName = "$libraryName"
    libraryDescription = 'UI Module'
    libraryVersion = "$coreVersion"
}

android {
    compileSdkVersion sdkCompileVersion
    buildToolsVersion buildTools
    defaultConfig {
        minSdkVersion sdkMinVersion
        targetSdkVersion sdkTargetVersion
        versionCode 1
        versionName version

        consumerProguardFiles 'proguard-rules.pro'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

    }
    buildTypes {
        debug {
            debuggable true
            buildConfigField CONFIG_TYPE_BOOLEAN, CONFIG_LOGGING_ENABLED, true.toString()
        }
        release {
            buildConfigField CONFIG_TYPE_BOOLEAN, CONFIG_LOGGING_ENABLED, false.toString()
        }
    }

    buildFeatures {
        dataBinding = true
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        warningsAsErrors true
        abortOnError false
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    })

    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"

    // Support Libraries
    implementation "androidx.recyclerview:recyclerview:$recyclerLibVersion"
    implementation "com.google.android.material:material:$materialLibVersion"

    // Chrome Custom Tabs
    implementation "androidx.browser:browser:$browserLibVersion"

    // Image Loading
    implementation "com.facebook.fresco:fresco:$frescoVersion"
    implementation "com.facebook.fresco:animated-gif:$frescoVersion"

    // Timber logging util
    implementation "com.jakewharton.timber:timber:$timberVersion"

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidTestVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
}

configurations.all() {
    resolutionStrategy.force "androidx.media:media:$mediaLibVersion"
    resolutionStrategy.force "androidx.legacy:legacy-support-v4:$legacySupportLibVersion"
}

apply from: rootProject.file('ossrh.gradle')
